buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

version = mod_version + "-B${System.getenv().BUILD_NUMBER}.${System.getenv().BUILD_DATE}-${minecraft_version}"
group = mod_group
archivesBaseName = "TeaStory"

minecraft {
    version = minecraft_version + "-" + forge_version
    runDir = "run"
    mappings = mcp_mapping

    replace "@version@", project.version
    makeObfSourceJar = true
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

artifacts {
    archives deobfJar
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

repositories {
    maven {
        // JEI
        name 'progwm\'s maven'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        // Hwyla && Guide-API
        url "http://tehnut.info/maven"
    }
    maven {
        // The One Probe
        url "https://maven.tterrag.com/"
    }
    maven {
        // Redstone Flux
        url = "http://maven.covers1624.net"
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:${theoneprobe_version}") {
        exclude group: 'cofh'
    }
    deobfCompile "info.amerifrance.guideapi:Guide-API:${guideapi_version}"
    compile files(
        'libs/ToughAsNails-1.12.2-3.1.0.129-universal.jar'
    )
}
